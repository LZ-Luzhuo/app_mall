if (isRelease) {
    apply plugin: 'com.android.library'
} else {
    apply plugin: 'com.android.application'
}

android {
    namespace 'me.luzhuo.app_mall'
    compileSdk sdk.compileSdk

    // 组件命名规范：命名以 mall_ 开头
    resourcePrefix 'mall_'
    defaultConfig {
        minSdk sdk.minSdk
        targetSdk sdk.targetSdk

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main {
            if (isRelease) {
                manifest.srcFile 'src\\main\\AndroidManifest.xml'
                java {
                    exclude '**/StartActivity.java'
                }
            } else {
                manifest.srcFile 'src\\main\\AndroidManifest_test.xml'
            }
        }
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    implementation project(path: ':lib_base')
    annotationProcessor 'com.alibaba:arouter-compiler:1.5.2'
}